@echo off
setlocal

IF NOT ISFUNCTION BATCHDIR function BATCHDIR=`%@IF[%@LEN[%_batchname]==0,%_CWD,%@LEFT[-1,%@PATH["%_batchname"]]]`

:: Check if the plugin is loaded
IFF NOT PLUGIN Hashmap THEN
    echo Loading hashmap plugin...^n
    plugin /L "%@BATCHDIR[]\..\build\cmake-build\hashmap.dll"
    set needUnload=1
ELSE
    set needUnload=0
ENDIFF

set h=%@hashnew[]

echo Adding entries...
set r=%@hashput[%h,foo/bar]
set r=%@hashput[%h,Bob Man/Mary Girl]
set r=%@hashput[%h,Super "Fly^]individual is "writing/Uglyman]
set r=%@hashput[%h,strange^nkey54/normal value]

echo Count = %@hashcount[%h]
echo -------------------------------------
hashentries "%h"
echo -------------------------------------^n

echo Keys:
echo -------------------------------------
hashentries /K %h
echo -------------------------------------^n

echo Values:
echo -------------------------------------
hashentries /V %h
echo -------------------------------------^n

echo Getting...
echo -------------------------------------
echo foo = %@hashget[%h,foo/(missing)]
echo baz = %@hashget[%h,baz/(baz doesn't exist, this is the default)]
echo Bob Man = %@hashget[%h,Bob Man]
echo -------------------------------------^n

echo Deleting Bob Man...
set r=%@hashdel[%h,Bob Man]
echo Count = %@hashcount[%h]
echo -------------------------------------
hashentries %h
echo -------------------------------------^n

echo Clearing...
set r=%@hashclear[%h]
echo Count = %@hashcount[%h]
echo -------------------------------------
hashentries %h
echo -------------------------------------^n
set r=%@hashfree[%h]

IFF DEFINED testHandles .AND. %testHandles != 0 THEN
    echo Allocating a bunch of handles...
    set n=40
    setarray handles[%n]
    echo -------------------------------------
    DO i = 0 TO %((%n-1))
        set handles[%i]=%@hashnew[]
        echos handles[%@FORMATN[02.0,%i]]=%handles[%i]
        IF %(((%i+1) MOD 5)) == 0 (echo ``) ELSE (echos ` `)
    ENDDO
    echo -------------------------------------
    echo Freeing handles...
    DO i = 0 TO %((%n-1)) (set r=%@hashfree[%handles[%i]])

    unsetarray handles
ENDIFF

:: testPerf variable is a bitmask (OR these together)
::  1 = test hashmap performance
::  2 = test UnQLite performance
IFF DEFINED testPerf THEN
    set numEntries=100
    
    IFF %((%testPerf & 1)) != 0 THEN
        set hfdb=%@BATCHDIR[]\perftest.hf
        echo ======== Hashmap ============
        set h=%@hashnew[16]
        echo Inserting %numEntries entries and saving to disk...
        TIMER /1 /Q ON
        DO i = 0 to %((%numEntries-1))
            set r=%@hashput[%h,key-%i/value-%i]
        ENDDO
        hashfile %h /W "%hfdb"
        echo The operation took %@TIMER[1,ms]^n
        TIMER /1 /M /Q OFF

        echo Retrieving %numEntries values...
        TIMER /1 /Q ON
        hashfile %h /R "%hfdb"
        DO i = 0 to %((%numEntries-1))
            set r=%@hashget[%h,key-%i]
            :: IF %r NE value-%i echo Value failure at key-%i
        ENDDO
        echo The operation took %@TIMER[1,ms]
        TIMER /1 /M /Q OFF
        set r=%@hashfree[%h]
        echo ==============================^n
    ENDIFF
    
    IFF %((%testPerf & 2)) != 0 THEN
        set unqdb=%@BATCHDIR[]\perftest.unqlite
        echo ======= Unqlite ==============
        set r=%@UNQOPEN[RWC,"%unqdb"]
        echo Inserting %numEntries entries into DB...
        TIMER /1 /Q ON
        DO i = 0 to %((%numEntries-1))
            set r=%@UNQKVS[u,"%unqdb","key-%i","value-%i"]
        ENDDO
        echo The operation took %@TIMER[1,ms]^n
        TIMER /1 /M /Q OFF
        set r=%@UNQCLOSE["%unqdb"]

        echo Retrieving %numEntries values...
        TIMER /1 /Q ON
        set r=%@UNQOPEN[MM,%unqdb]
        DO i = 0 to %((%numEntries-1))
            set r=%@UNQREADS[u,%unqdb,"key-%i"]
        ENDDO
        echo The operation took %@TIMER[1,ms]
        TIMER /1 /M /Q OFF
        set r=%@UNQCLOSE[%unqdb]
        echo ==============================^n
    ENDIFF
ENDIFF

:unload
IF %needUnload==1 (echo ^nUnloading hashmap plugin... & plugin /U hashmap)

endlocal
